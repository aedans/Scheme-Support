{
  parserClass="com.aedans.plugins.scheme.lang.parser.SchemeParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Scheme"
  psiImplClassSuffix="Impl"
  psiPackage="com.aedans.plugins.scheme.lang.psi.gen"
  psiImplPackage="com.aedans.plugins.scheme.lang.psi.gen.impl"
  psiImplUtilClass="com.aedans.plugins.scheme.lang.psi.SchemePsiImplUtil"

  elementTypeHolderClass="com.aedans.plugins.scheme.lang.lexer.SchemeTokenTypes"
  elementTypeClass="com.aedans.plugins.scheme.lang.lexer.SchemeElementType"
  tokenTypeClass="com.aedans.plugins.scheme.lang.lexer.SchemeTokenType"
}

simpleFile ::= form*

form ::= definition|expression

// Definitions
definition ::= variable_definition
             | syntax_definition
             | begin_definition
             | let_definition
             | letrec_definition

variable_definition ::= OPEN_PAREN DEFINE id expression CLOSE_PAREN
                      | OPEN_PAREN DEFINE OPEN_PAREN id id* CLOSE_PAREN body CLOSE_PAREN
                      | OPEN_PAREN DEFINE OPEN_PAREN id id* DOT id CLOSE_PAREN body CLOSE_PAREN

syntax_definition ::= OPEN_PAREN DEFINE_SYNTAX id expression CLOSE_PAREN

begin_definition ::= OPEN_PAREN BEGIN definition* CLOSE_PAREN

let_definition ::= OPEN_PAREN LET_SYNTAX OPEN_PAREN syntax_binding* definition* CLOSE_PAREN

letrec_definition ::= OPEN_PAREN LETREC_SYNTAX OPEN_PAREN syntax_binding* CLOSE_PAREN definition* CLOSE_PAREN

body ::= definition* expression+

syntax_binding ::= OPEN_PAREN id expression CLOSE_PAREN

// Expressions
expression ::= constant
             | id
             | quote_expression
             | lambda_expression
             | if_expression
             | set_expression
             | application
             | let_syntax_expression
             | letrec_syntax_expression
             | syntax_rules_expression
             | begin_expression
             | abbreviation

quote_expression ::= OPEN_PAREN QUOTE datum CLOSE_PAREN

lambda_expression ::= OPEN_PAREN LAMBDA formals body CLOSE_PAREN

if_expression ::= OPEN_PAREN IF expression expression expression CLOSE_PAREN
                | OPEN_PAREN IF expression expression CLOSE_PAREN

set_expression ::= SET id expression

let_syntax_expression ::= OPEN_PAREN LET_SYNTAX OPEN_PAREN syntax_binding* CLOSE_PAREN expression+ CLOSE_PAREN

letrec_syntax_expression ::= OPEN_PAREN LETREC_SYNTAX OPEN_PAREN syntax_binding* CLOSE_PAREN expression+ CLOSE_PAREN

syntax_rules_expression ::= OPEN_PAREN SYNTAX_RULES list expression+ CLOSE_PAREN

begin_expression ::= OPEN_PAREN BEGIN expression* CLOSE_PAREN

constant ::= BOOLEAN|NUMBER|CHARACTER|STRING|nil

nil ::= OPEN_PAREN CLOSE_PAREN

formals ::= id
          | OPEN_PAREN id* CLOSE_PAREN
          | OPEN_PAREN id+ DOT id CLOSE_PAREN

application ::= OPEN_PAREN expression expression* CLOSE_PAREN

// datum
datum ::= BOOLEAN|NUMBER|CHARACTER|STRING|symbol|list|vector

symbol ::= IDENTIFIER

list ::= OPEN_PAREN datum* CLOSE_PAREN
       | OPEN_PAREN datum+ DOT datum CLOSE_PAREN
       | abbreviation

abbreviation ::= APOSTROPHE datum
               | BACKQUOTE datum
               | COMMA datum
               | COMMA_AT datum

vector ::= POUND OPEN_PAREN datum* CLOSE_PAREN

id ::= IDENTIFIER
